version = '2.0.0-SNAPSHOT'

processResources {

    def expandedFiles = ['config/application.properties']

    from ('src/main/resources') {
        include expandedFiles
        expand(project.properties)
    }
    from ('src/main/resources') {
        exclude expandedFiles
    }

    into 'build/main/resources'
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

dependencies {

    compileOnly project(':api')
    compileOnly project(':jworkspace')

    implementation project(':ui-api')
    implementation project(':widgets')

    implementation "org.jdom:jdom2:2.0.6.1"
    testImplementation project(':jworkspace')
}

jar {
    manifest {
         attributes([
                 "PluginName": jar.archiveBaseName.get(),
                 "PluginType": "ANY",
                 "PluginDescription": jar.archiveClassifier.get(),
                 "PluginIcon": "jworkspace/ui/images/desktop/desktop.png",
                 "PluginVersion": jar.archiveVersion.get(),
                 "PluginHelpURL": "https://github.com/grauds/clematis.desktop/tree/master/modules/ui"
         ], "jworkspace.ui.WorkspaceGUI.class")
    }
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

tasks.register('deployToPlugins', Copy) {
    from jar
    //into "${System.getProperty("user.home")}/.jworkspace/plugins/"
    into "${project.rootDir}/.jworkspace/plugins/"
}
