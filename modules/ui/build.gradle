version '2.0.0-SNAPSHOT'

processResources {

    def expandedFiles = ['config/application.properties']

    from ('src/main/resources') {
        include expandedFiles
        expand(project.properties)
    }
    from ('src/main/resources') {
        exclude expandedFiles
    }

    into 'build/main/resources'
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

ext {
    codecVersion = '1.11'
    jdomVersion = '2.0.6'
}

dependencies {

    compileOnly project(':jworkspace')
    testImplementation project(':jworkspace')

    implementation "commons-codec:commons-codec:$codecVersion"
    implementation "org.jdom:jdom2:$jdomVersion"
}

jar {
    manifest {
         attributes([
                 "PluginName": jar.archiveBaseName.get(),
                 "PluginType": "ANY",
                 "PluginDescription": jar.archiveClassifier.get(),
                 "PluginIcon": "",
                 "PluginVersion": jar.archiveVersion.get(),
                 "PluginHelpURL": ""
         ], "jworkspace.ui.WorkspaceGUI.class")
    }
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

task deployToPlugins(type: Copy) {
    from jar
    into "${System.getProperty("user.home")}/.jworkspace/plugins/"
}

deployToPlugins.dependsOn(assemble)
build.finalizedBy deployToPlugins
