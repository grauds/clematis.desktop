version = '2.0.0-SNAPSHOT'

processResources {

    def expandedFiles = ['config/application.properties']

    from ('src/main/resources') {
        include expandedFiles
        expand(project.properties)
    }
    from ('src/main/resources') {
        exclude expandedFiles
    }

    into 'build/main/resources'
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE

}

dependencies {

    compileOnly project(':jworkspace')
    compileOnly project(':ui-api')
    compileOnly project(':ui')
    compileOnly "org.jdom:jdom2:2.0.6.1"

    implementation project(':widgets')
    implementation project(':runtime')

    implementation "joda-time:joda-time:2.10.3"

    testImplementation project(':ui')
}

jar {
    manifest {
         attributes([
                 "PluginName": jar.archiveBaseName.get(),
                 "PluginLevel": "SYSTEM",
                 "PluginType": "UI",
                 "PluginDescription": jar.archiveClassifier.get(),
                 "PluginIcon": "jworkspace/ui/runtime/images/runtime.png",
                 "PluginIconBig": "jworkspace/ui/runtime/images/runtime_big.png",
                 "PluginVersion": jar.archiveVersion.get(),
                 "PluginHelpURL": "https://github.com/grauds/clematis.desktop/tree/master/modules/rtui"
         ], "jworkspace.ui.runtime.RuntimeManagerWindow.class")
    }
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

tasks.register('deployToPlugins', Copy) {
    from jar
    into "${project.rootDir}/.jworkspace/shells/"
}

deployToPlugins.dependsOn(assemble)
build.finalizedBy deployToPlugins
