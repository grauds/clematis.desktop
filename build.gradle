buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        jcenter()
	    maven {
            url 'http://central.maven.org/maven2/'
        }
    }
}

plugins {
    id 'jacoco'
}

group 'name.troshin.anton.clematis'
version '2.0.0 - DEV'

allprojects {
    apply plugin: 'java'
    apply plugin: 'findbugs'
    apply plugin: 'checkstyle'
    apply plugin: 'jacoco'
}

test {
    jacoco {
        append = false
        destinationFile = file("$buildDir/jacoco/jacocoTest.exec")
        classDumpDir = file("$buildDir/jacoco/classpathdumps")
    }
}

subprojects {

    apply from: file("${project.rootDir}/dependencies.gradle")
    apply from: file("${project.rootDir}/libraries.gradle")

    apply plugin: 'idea'
    apply plugin: 'eclipse'

    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'

    compileJava {
       // options.compilerArgs << '-Xlint:unchecked' << "-Xlint:deprecation"
        options.compilerArgs << '-Xlint:sunapi' << '-XDenableSunApiLintControl'
    }

    repositories {
        jcenter()
    }

    task codeCoverageReport(type: JacocoReport) {
        executionData fileTree(project.rootDir.absolutePath).include("**/build/jacoco/*.exec")

        allprojects.each {
            sourceSets it.sourceSets.main
        }

        reports {
            xml.enabled true
            xml.destination file("${buildDir}/reports/jacoco/report.xml")
            html.enabled true
            csv.enabled false
        }
    }

// always run the tests before generating the report
    codeCoverageReport.dependsOn {
        subprojects.test
    }

    jar {
        archiveFileName = "${archiveBaseName}.${archiveExtension}"

        manifest {
            attributes(
                    "Implementation-Title": project.name,
                    "Implementation-Version": archiveVersion
            )
        }
    }

    findbugs {
        toolVersion = findbugsVersion
        sourceSets = [sourceSets.main]
    }

    findbugsMain {
        reports {
            html.enabled = true
            xml.enabled = false
        }
    }

    checkstyle {
        configProperties.configFile = file("${project.rootDir}/config/checkstyle/checkstyle.xml")
        configProperties.checkstyleSuppressionFile = file("${project.rootDir}/config/checkstyle/suppressions.xml")
    }

    task checkstyleReport {
        doLast {
            if (file("${buildDir}/reports/checkstyle/${checkType}.xml").exists()) {
                ant.xslt(in: "${buildDir}/reports/checkstyle/${project.ext.checkType}.xml",
                        style: "${project.rootDir}/config/checkstyle/checkstyle.xsl",
                        out: "${buildDir}/reports/checkstyle/checkstyle_${project.ext.checkType}.html")
            }
        }
    }

    tasks.withType(FindBugs) {
        exclude '**/*Test*'
    }

    tasks.withType(Checkstyle) {
        exclude '**/*Test*'
    }

    build {
        dependsOn tasks.codeCoverageReport
    }
}
